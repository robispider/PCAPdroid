apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    ndkVersion '26.1.10909125'

    defaultConfig {
        applicationId "com.emanuelef.remote_capture"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 71
        versionName "1.6.8"

        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            // Use a different app ID for debug to allow its coexistence with release builds
            applicationIdSuffix '.debug'
            versionNameSuffix '-beta'
            pseudoLocalesEnabled true

            if(project.hasProperty("doNotStrip")) {
                // NOTE: packagingOptions will also affect the "release" build, so the "doNotStrip"
                // guard is required
                packagingOptions {
                    // NOTE: unstripped nDPI library takes up about 4 MB!
                    //doNotStrip '**.so'
                    doNotStrip '**/libpcapd.so'
                    doNotStrip '**/libcapture.so'
                }
            }
        }
    }

    packagingOptions {
        jniLibs {
            // NOTE: needed to unpack libpcapd.so when installed as app bundle
            useLegacyPackaging = true
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.22.1"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += '../submodules/MaxMind-DB-Reader-java/src/main/java'
    }

    testOptions {
        // needed by robolectric
        unitTests.includeAndroidResources = true
    }
    namespace 'com.emanuelef.remote_capture'
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10"
   // implementation ('some.library:library:version') {
  //      exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
  //      exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
 //   }

    implementation 'org.chromium.net:cronet-embedded:113.5672.61'

// Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation "org.robolectric:robolectric:4.10.3"

// AndroidX
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment:1.5.7'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation "androidx.navigation:navigation-fragment:2.5.3"
    implementation "androidx.navigation:navigation-ui:2.5.3"

// Google
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.brotli:dec:0.1.2'

// Third-party
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'com.github.KaKaVip:Android-Flag-Kit:v0.1'
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation 'com.github.androidmads:QRGenerator:1.0.1'
}
